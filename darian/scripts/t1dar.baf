// ----------------------------------
// Darian's override script
// ----------------------------------

// ---------------------------------------------
// Romance & friendship checks
// ---------------------------------------------
// PC Romance
IF
    InParty(Myself)
    Global("T1DarMatchCheck","GLOBAL",0)
    Gender(Player1,FEMALE)
    !Alignment(Player1,MASK_EVIL)
    OR(3)                             
      Race(Player1,ELF)
      Race(Player1,HALF_ELF)
      Race(Player1,HUMAN)
THEN
  RESPONSE #100
    SetGlobal("T1DarMatchCheck","GLOBAL",1)
    SetGlobal("T1DarMatch","GLOBAL",1)
    SetGlobal("T1DarRomanceActive","GLOBAL",1)
    SetGlobal("T1DarLovetalk","GLOBAL",1)                 // Setting the variable for the first lovetalk
    RealSetGlobalTimer("T1DarRomanceTimer","GLOBAL",3600) // Setting the timer for first lovetalk
END


// ---------------------------------------------
// PC Romance
// ---------------------------------------------
// Romance Dialogues                                      // This is where the meat of the romance is, of course!
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T1DarRomanceTimer","GLOBAL")  // If the timer has run out: remember, with timers, you have to
    CombatCounter(0)                                      // have set them first.
    !See([ENEMY])
    OR(4)
      Global("T1DarRomanceActive","GLOBAL",1)
      Global("T1DarRomanceActive","GLOBAL",2)
      Global("T1DarRomanceActive","GLOBAL",4)
      Global("T1DarRomanceActive","GLOBAL",5)
    OR(16)
      Global("T1DarLovetalk","GLOBAL",2)
      Global("T1DarLovetalk","GLOBAL",4)
      Global("T1DarLovetalk","GLOBAL",6)
      Global("T1DarLovetalk","GLOBAL",8)
      Global("T1DarLovetalk","GLOBAL",10)
      Global("T1DarLovetalk","GLOBAL",12)
      Global("T1DarLovetalk","GLOBAL",14)
      Global("T1DarLovetalk","GLOBAL",16)
      Global("T1DarLovetalk","GLOBAL",18)
      Global("T1DarLovetalk","GLOBAL",20)
      Global("T1DarLovetalk","GLOBAL",22)
//      Global("T1DarLovetalk","GLOBAL",24)               // This one is in the dream script (t1dard.baf)
      Global("T1DarLovetalk","GLOBAL",26)
      Global("T1DarLovetalk","GLOBAL",28)
      Global("T1DarLovetalk","GLOBAL",30) 
      Global("T1DarLovetalk","GLOBAL",32)
      Global("T1DarLovetalk","GLOBAL",34)
THEN
  RESPONSE #100
    MoveToObject(Player1)                                 // Move to the PC
    PlaySong(0)
    PlaySound("t1darrom")
    StartDialogueNoSet(Player1)                           // Start dialogue with PC (J file)
END                                                       // which nowadays is usually used for Lovetalks and any other talks which
                                                          // are triggered by script (friendtalks, scenery talks, etc)
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T1DarRomanceTimer","GLOBAL")  // If the timer we set in the dialogue has run out
    CombatCounter(0)
    !See([ENEMY])
    OR(15)                                                // If Lovetalk is at an even number (set in the dialogue)
      Global("T1DarLovetalk","GLOBAL",1)
      Global("T1DarLovetalk","GLOBAL",3)
      Global("T1DarLovetalk","GLOBAL",5)
      Global("T1DarLovetalk","GLOBAL",7)
      Global("T1DarLovetalk","GLOBAL",9)
      Global("T1DarLovetalk","GLOBAL",11)
      Global("T1DarLovetalk","GLOBAL",13)
      Global("T1DarLovetalk","GLOBAL",15)
      Global("T1DarLovetalk","GLOBAL",17)
      Global("T1DarLovetalk","GLOBAL",19)
      Global("T1DarLovetalk","GLOBAL",21)
      Global("T1DarLovetalk","GLOBAL",23)
      Global("T1DarLovetalk","GLOBAL",25)
      Global("T1DarLovetalk","GLOBAL",27)
      Global("T1DarLovetalk","GLOBAL",29)
THEN
  RESPONSE #100
    IncrementGlobal("T1DarLovetalk","GLOBAL",1)           // increment Lovetalk by 1 to say we're ready for a lovetalk! Return to
END                                                       // above script. Wash, rinse, repeat.


// Romance only lovetalks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T1DarRomanceTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    OR(2)
      Global("T1DarRomanceActive","GLOBAL",2)
      Global("T1DarRomanceActive","GLOBAL",4)
    OR(2)
      Global("T1DarLovetalk","GLOBAL",31)
      Global("T1DarLovetalk","GLOBAL",33)
THEN
  RESPONSE #100
    IncrementGlobal("T1DarLovetalk","GLOBAL",1)
END


// Spellhold talk
IF
    InParty(Myself)                                       // This block is for the Spellhold talk, only initiated if the player
    See(Player1)                                          // is in a romantic relationship with Darian.
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR1512")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarRomanceActive","GLOBAL",4)
    Global("T1DarSpellholdCheck","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1DarSpellholdTimer","GLOBAL",30) // Sets a short (30-second) timer so it doesn't occur right away
    SetGlobal("T1DarSpellholdCheck","GLOBAL",1)           // Ensures this block won't repeat; doesn't need to be a GLOBAL,
END                                                       // so I've made it an area variable.

IF
    RealGlobalTimerExpired("T1DarSpellholdTimer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR1512")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarSpellholdCheck","GLOBAL",1)
    Global("T1DarSpellholdTalk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T1DarSpellholdTalk","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1DarSpellholdTimer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR1512")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarSpellholdTalk","GLOBAL",1)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("t1darrom")
    StartDialogueNoSet(Player1)
END


// After Underdark scene
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2500")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarRomanceActive","GLOBAL",2)
    Global("T1DarUnderExitCheck","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1DarUnderExitTimer","GLOBAL",60)
    SetGlobal("T1DarUnderExitCheck","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1DarUnderExitTimer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2500")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarUnderExitCheck","GLOBAL",1)
    Global("T1DarUnderExitTalk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T1DarUnderExitTalk","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1DarUnderExitTimer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2500")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1DarUnderExitTalk","GLOBAL",1)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("t1darrom")
    StartDialogueNoSet(Player1)
END


// Preventing LTs from appearing when not wanted
IF
    Global("T1DarRomanceActive","GLOBAL",3)
    GlobalGT("T1DarLovetalk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T1DarLovetalk","GLOBAL",0)
END


// ---------------------------------------------
// Darian initiated PC Flirts
// ---------------------------------------------
IF
    Global("T1DarFlirtsCheck","GLOBAL",0)                 // This section probably isn't too hard to understand--just follow the
    InParty(Myself)                                       // variables through the script and dialogue. :)
    OR(2)
      Global("T1DarRomanceActive","GLOBAL",1)
      Global("T1DarRomanceActive","GLOBAL",2)
    OR(2)
      GlobalGT("T1DarLoveTalk","GLOBAL",4)
      GlobalGT("T1DarBondedTalk","GLOBAL",4)
THEN
  RESPONSE #100
    SetGlobal("T1DarFlirtsCheck","GLOBAL",1)             // Starting the flirts!
    RealSetGlobalTimer("T1DarFlirtTimer","GLOBAL",1800)
END

IF
    RealGlobalTimerExpired("T1DarFlirtTimer","GLOBAL")   // Once timer has run out,
    InParty(Myself)
    Global("T1DarTimeForFlirt","GLOBAL",0)               // but if TimeForFlirt=0
    OR(2)
        Global("T1DarRomanceActive","GLOBAL",2)
        Global("T1DarRomanceActive","GLOBAL",4)
    OR(2)                                                // and we're in the correct chapters for flirting (not end, not underdark)
        GlobalLT("Chapter","GLOBAL",4)
        Global("Chapter","GLOBAL",6)
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("T1DarTimeForFlirt","GLOBAL",1)            // It's time to flirt!
END

IF
    Global("T1DarTimeForFlirt","GLOBAL",1)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)                          // aaand to the flirts :)
END


// ---------------------------------------------
// Area Interjections
// ---------------------------------------------
// Graveyard
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0800")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_graveyard","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1dar_graveyard_timer","GLOBAL",120)
    SetGlobal("T1dar_graveyard","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1dar_graveyard_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0800")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_graveyard","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("T1dar_graveyard","GLOBAL",2)
END

IF
    RealGlobalTimerExpired("T1dar_graveyard_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0800")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_graveyard","GLOBAL",2)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// Entering the Underdark
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2100")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_underdark","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1dar_underdark_timer","GLOBAL",120)
    SetGlobal("T1dar_underdark","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1dar_underdark_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2100")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_underdark","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("T1dar_underdark","GLOBAL",2)
END

IF
    RealGlobalTimerExpired("T1dar_underdark_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2100")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_underdark","GLOBAL",2)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END         

// Planar Sphere - Hell plane
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0414")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_sphere","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1dar_sphere_timer","GLOBAL",30)
    SetGlobal("T1dar_sphere","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1dar_sphere_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0414")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_sphere","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("T1dar_sphere","GLOBAL",2)
END

IF
    RealGlobalTimerExpired("T1dar_sphere_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR0414")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_sphere","GLOBAL",2)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// Druid Grove
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    OR(2)
      AreaCheck("AR1900")
      AreaCheck("AR1901")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_druidgrove","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("T1dar_druidgrove_timer","GLOBAL",30)
    SetGlobal("T1dar_druidgrove","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("T1dar_druidgrove_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    OR(2)
      AreaCheck("AR1900")
      AreaCheck("AR1901")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_druidgrove","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("T1dar_druidgrove","GLOBAL",2)
END

IF
    RealGlobalTimerExpired("T1dar_druidgrove_timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    OR(2)
      AreaCheck("AR1900")
      AreaCheck("AR1901")
    CombatCounter(0)
    !See([ENEMY])
    Global("T1dar_druidgrove","GLOBAL",2)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END